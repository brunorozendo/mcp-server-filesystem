plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.1'
}

group = 'com.brunorozendo'
version = '1.0.0'


java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':tools')
    implementation 'io.modelcontextprotocol.sdk:mcp:0.15.0'

}

application {
    mainClass = 'com.brunorozendo.mcp.filesystem.FilesystemServer'
}


jar {
    manifest {
        attributes(
                'Main-Class': application.mainClass.get()
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Exclude signature files to avoid security exceptions
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}


graalvmNative {
    useArgFile = false
    toolchainDetection = true
    binaries {
        main {
            imageName = 'mcp-server-filesystem'
            mainClass = application.mainClass.get()
            buildArgs.add('--no-fallback')
            buildArgs.add('-H:+ReportExceptionStackTraces')
            buildArgs.add('--initialize-at-build-time=org.slf4j')
            buildArgs.add('--initialize-at-build-time=ch.qos.logback')
            // Configuration files will be picked up automatically from META-INF/native-image
        }
    }
}